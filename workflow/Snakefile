# Title: Snakefile
# Author: Amanda Zacharias
# Date: 2023-06-01
# Email: 16amz1@queensu.ca
#-------------------------------------------------
# Notes -------------------------------------------
# source activate ~/mareesTutorialGWAS/workflow/envs/mareesGWAS
# module load StdEnv/2020 python/3.11.2
# Example executions:
# snakemake --cores 1 --use-conda
# snakemake --slurm --jobs 2 --use-conda
# snakemake --slurm --jobs 10 --rerun-incomplete --use-conda
# Visualize dag: 
# snakemake --dag | dot -Tpdf > dag.pdf

# Load packages
import os
from os.path import join

# Load rules
include: join("rules", "missing.smk")
include: join("rules", "checkSex.smk")

rule all:
    input:
        # MAF report
        join("results", "1_qcGWAS", "3_filtSNPs", "checkMAF.frq")
        
rule filterAutosomal: 
    input: 
        inPlink = expand(join("results", "1_qcGWAS", "2_checkSex", "HapMap_impSex.{end}"), 
          end = ["bed", "bim", "fam"])
    output: 
        outFiles = expand(join("results", "1_qcGWAS", "3_filtSNPs", "HapMap_auto.{end}"), 
          end = ["bed", "bim", "fam"])
    params: 
        bfilePath = join("results", "1_qcGWAS", "2_checkSex", "HapMap_impSex"), 
        outPrefix = join("results", "1_qcGWAS", "3_filtSNPs", "HapMap_auto")
    log: 
        out = join("logs", "1_qcGWAS", "3_autosomal.out"), 
        err = join("logs", "1_qcGWAS", "3_autosomal.err")
    resources: 
        mem_mb = 30000, 
        runtime = 100, 
        cpus_per_task = 1, 
        slurm_extra = "--qos=privileged --mail-type=END,FAIL --mail-user=16amz1@queensu.ca"
    shell: 
        """
        module load StdEnv/2020
        module load plink/1.9b_6.21-x86_64
        sh scripts/filterAutosomal.sh -b {params.bfilePath} -o {params.outPrefix} \
            1> {log.out} 2> {log.err}
        """         

rule checkMAF: 
    input: 
        inPlink = expand(join("results", "1_qcGWAS", "3_filtSNPs", "HapMap_auto.{end}"), 
          end = ["bed", "bim", "fam"])
    output: 
        outFile = join("results", "1_qcGWAS", "3_filtSNPs", "checkMAF.frq")
    params: 
        bfilePath = join("results", "1_qcGWAS", "3_filtSNPs", "HapMap_auto"),
        outPrefix = join("results", "1_qcGWAS", "3_filtSNPs", "checkMAF")
    log: 
        out = join("logs", "1_qcGWAS", "3_checkMAF.out"), 
        err = join("logs", "1_qcGWAS", "3_checkMAF.err")
    resources: 
        mem_mb = 30000, 
        runtime = 100, 
        cpus_per_task = 1, 
        slurm_extra = "--qos=privileged --mail-type=END,FAIL --mail-user=16amz1@queensu.ca"
    shell: 
        """
        module load StdEnv/2020
        module load plink/1.9b_6.21-x86_64
        sh scripts/checkMAF.sh -b {params.bfilePath} -o {params.outPrefix} \
            1> {log.out} 2> {log.err}
        """  

